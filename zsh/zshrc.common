# Aliases
alias lg='lazygit'
alias v='nvim'
alias oc='opencode'
alias ls='eza'
alias cls='clear && tmux clear-history'
alias reload='source ~/.zshrc'

# zk notebook directory
export ZK_NOTEBOOK_DIR="$HOME/md"

# Git shortcuts (all support -h flag to prepend HUSKY=0)
_git_default_branch() {
  git symbolic-ref refs/remotes/origin/HEAD 2>/dev/null | sed 's@^refs/remotes/origin/@@' || echo "master"
}

_git_husky() {
  local husky="$1"
  shift
  if [[ "$husky" == "-h" ]]; then
    HUSKY=0 git "$@"
  else
    git "$@"
  fi
}

glg() {
  if [[ "$1" == "-a" ]]; then
    shift
    git log --oneline --graph --decorate --all "$@"
  else
    git log --oneline --graph --decorate "$@"
  fi
}

gfom() { _git_husky "$1" fetch origin "$(_git_default_branch)"; }
grbom() { _git_husky "$1" rebase "origin/$(_git_default_branch)"; }
grthom() { _git_husky "$1" reset --hard "origin/$(_git_default_branch)"; }

grbi() {
  local h=""
  [[ "$1" == "-h" ]] && h=1 && shift
  [[ -z "$1" ]] && echo "Usage: grbi [-h] <n>" && return 1
  if [[ -n "$h" ]]; then HUSKY=0 git rebase -i "HEAD~$1"; else git rebase -i "HEAD~$1"; fi
}

grts() {
  local h=""
  [[ "$1" == "-h" ]] && h=1 && shift
  [[ -z "$1" ]] && echo "Usage: grts [-h] <n>" && return 1
  if [[ -n "$h" ]]; then HUSKY=0 git reset --soft "HEAD~$1"; else git reset --soft "HEAD~$1"; fi
}

# Zsh options
setopt AUTO_CD
setopt EXTENDED_GLOB

# Skip optional git locks for read-only operations (prevents lock contention
# between starship prompt, tmux2k status bar, and neovim gitsigns)
export GIT_OPTIONAL_LOCKS=0

# Completion styles (zsh-autocomplete handles compinit)
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# Disable real-time completion list (only show menu on Shift+Tab)
zstyle ':autocomplete:*:*' list-lines 0



# Keybindings for completion (applied after plugins load)
# Tab: standard completion, Shift+Tab: open menu, Escape: cancel menu
# Up/Down: simple history navigation (zsh-autocomplete overrides these with
# fuzzy history search that conflicts with list-lines 0 setting)
zvm_after_init() {
  bindkey '^I' complete-word        # Tab
  bindkey '^[[Z' menu-select        # Shift+Tab
  bindkey -M menuselect '^[' send-break  # Escape cancels menu
  bindkey '^[[A' up-line-or-history       # Up arrow
  bindkey '^[[B' down-line-or-history     # Down arrow
  bindkey -r '^[^[[A'                     # Remove Alt+Up (zsh-autocomplete history-search)
  bindkey -r '^[^[[B'                     # Remove Alt+Down (zsh-autocomplete menu-select)
  bindkey -r '^[^[OA'                     # Remove Alt+Up variant
  bindkey -r '^[^[OB'                     # Remove Alt+Down variant
}

# NVM default node on PATH eagerly (~0ms, avoids full nvm init ~2.5s)
export NVM_DIR="$HOME/.nvm"
_nvm_default=$(cat "$NVM_DIR/alias/default" 2>/dev/null)
[[ "$_nvm_default" != v* ]] && _nvm_default="v$_nvm_default"
export PATH="$NVM_DIR/versions/node/$_nvm_default/bin:$PATH"
unset _nvm_default

# NVM (lazy-loaded)
_nvm_load() {
  unset -f nvm node npm npx yarn pnpm _nvm_load
  [ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"
  [ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"
}
nvm() { _nvm_load; nvm "$@"; }
node() { _nvm_load; node "$@"; }
npm() { _nvm_load; npm "$@"; }
npx() { _nvm_load; npx "$@"; }
yarn() { _nvm_load; yarn "$@"; }
pnpm() { _nvm_load; pnpm "$@"; }

# Midnight Commander with auto light/dark theme
# Note: vf (vifm wrapper) is a script in ~/dev/setup/scripts/vf
function mc() {
  local skin
  if [[ "$(defaults read -g AppleInterfaceStyle 2>/dev/null)" == "Dark" ]]; then
    skin="dark"
  else
    skin="light"
  fi
  command mc -S "$skin" "$@"
}

# zsh-vi-mode config (must be before sourcing the plugin)
ZVM_INIT_MODE=sourcing  # init immediately, not deferred to precmd
ZVM_CURSOR_STYLE_ENABLED=true
ZVM_INSERT_MODE_CURSOR=$ZVM_CURSOR_BLINKING_BEAM
ZVM_NORMAL_MODE_CURSOR=$ZVM_CURSOR_BLOCK
ZVM_VI_HIGHLIGHT_BACKGROUND=#3a3a3a
ZVM_LINE_INIT_MODE=$ZVM_MODE_INSERT

# Reset to insert mode on Ctrl+C
TRAPINT() {
  zvm_enter_insert_mode
  return $(( 128 + $1 ))
}

